plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.0.1" apply true
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group 'sh.miles.raven'
version '2023.5.17'
description 'Database API by Miles'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = "UTF-8"
jar.enabled = false 

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    
    plugins.withType(JavaPlugin) {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        dependencies {
            implementation 'com.google.guava:guava:31.1-jre'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    api project(':api')
    implementation project(':core') 
    implementation project(':data-mapper')
    implementation project(':database-providers')
    implementation project(':database-providers:mongodb')
    implementation project(':database-providers:nitrite')
}

shadowJar {
    archiveClassifier.set('')
    mustRunAfter(':api:shadowJar')
    mustRunAfter(':core:shadowJar')
    mustRunAfter(':data-mapper:shadowJar')
    mustRunAfter(':database-providers:shadowJar')
    mustRunAfter(':database-providers:mongodb:shadowJar')
    mustRunAfter(':database-providers:nitrite:shadowJar')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            repositories {
                maven {
                    credentials {
                        username = "$milesMavenUser"
                        password = "$milesMavenPass"
                    }
                    url = "https://maven.miles.sh/libraries"
                }
            }
        }
    }
}


